plugins {
    id 'java-library'
    id 'maven'
    id 'signing'
}

group = "io.jddf.gson"
archivesBaseName = "jddf-gson"
version = "0.1.0"

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
}

dependencies {
    implementation 'com.google.code.gson:gson:2.8.5'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
}

signing {
    sign configurations.archives
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'jddf-gson'
        packaging 'jar'
        description 'JSON Data Definition Format validation with Gson'
        url 'https://github.com/jddf/jddf-java'

        scm {
          connection 'scm:git:https://github.com/jddf/jddf-java'
          developerConnection 'scm:git:https://github.com/jddf/jddf-java'
          url 'https://github.com/jddf/jddf-java'
        }

        licenses {
          license {
            name 'The MIT License'
            url 'https://opensource.org/licenses/MIT'
          }
        }

        developers {
          developer {
            id 'ucarion'
            name 'Ulysse Carion'
            email 'ulysse@segment.com'
          }
        }
      }
    }
  }
}
